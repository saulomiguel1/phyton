Algoritmo "JOGODAVELHA "

Var
   v: vetor [1..3, 1..3] de caractere
   l, c, cont, po: inteiro
   simb: caractere
   R: LOGICO


Procedimento MostraVelha()
Inicio
      Escreval ("+---+---+---+")
      Para l <- 1 ate 3 faca
           Para c <- 1 ate 3 faca
                Escreva ("|  ", v[l,c]:1)
           FimPara
           Escreva ("|")
           Escreval ()
           Escreval ("+---+---+---+")
      FimPara
FimProcedimento


Procedimento mudaJogador()
Inicio
      Se (simb = "X") entao
         simb <- "O"
      senao
           simb <- "X"
      FimSe
FimProcedimento


Funcao jogar (S: caractere ; P: inteiro): logico
Var
mudou: logico
Inicio
      mudou <- falso
      Para l <- 1 ate 3 faca
           Para c <- 1 ate 3 faca
                Se  (v[l,c] = NumpCarac(P)) entao
                    v[l,c] <- S
                    mudou <- verdadeiro
                FimSe
           FimPara
      FimPara
      Retorne mudou
FimFuncao


Funcao TerminouVelha (): logico
Var
   terminou: logico
   ocorr: inteiro
Inicio
      terminou <- falso
      Para l <- 1 ate 3 faca
           Se (v[l,1] = v[l,2]) e (v[l,2] = v[l,3]) entao
              terminou <- verdadeiro
           FimSe
      FimPara
      
      Para c <- 1 ate 3 faca
           Se (v[1,c] = v[2,c]) e (v[2,c] = v[3,c]) entao
              terminou <- verdadeiro
           FimSe
      FimPara
      
      Se (v[1,1] = v[2,2]) e (v[2,2] = v[3,3]) entao
         terminou <- verdadeiro
      FimSe
      Se (v[1,3] = v[2,2]) e (v[2,2] = v[3,1]) entao
         terminou <- verdadeiro
      FimSe
      
      ocorr <- 0
      Para l <- 1 ate 3 faca
           Para c <- 1 ate 3 faca
                Se (v[l,c] <> "X") e (v[l,c] <> "O") entao
                   ocorr <- ocorr + 1
                FimSe
           FimPara
      FimPara
      Se (ocorr = 0) entao
         terminou <- verdadeiro
      FimSe
      
      Retorne terminou
FimFuncao


Inicio
      cont <- 1
      simb <- "X"
      Para l <- 1 ate 3 faca
           Para c <- 1 ate 3 faca
                v[l,c] <- NumpCarac (cont)
                cont <- cont + 1
           FimPara
      FimPara
      MostraVelha()
      Repita
            Repita
            Escreval ("Vai jogar [", simb:1, "] em qual posição?")
            Leia (po)
            R <- jogar (simb, po))
              Se (R = falso) entao
                 Escreval ("JOGADA INVÁLIDA!")
              FimSe
            Ate (R = verdadeiro)
            mudaJogador ()
            Limpatela
            MostraVelha()
      Ate (terminouVelha = verdadeiro)
      Escreval ("JOGO FINALIZADO!")
Fimalgoritmo